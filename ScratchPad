let binSize = 24h;
let timeRangeStart = now(-30d);
let timeRangeEnd = now();
let filterSubstring = "M14"; // Replace with the desired substring, or set to "ALL" for all computers
let timezoneOffsetHours = 8; // Offset for UTC+8
Perf
| where TimeGenerated between (timeRangeStart..timeRangeEnd)
| extend LocalTime = datetime_add('hour', timezoneOffsetHours, TimeGenerated) // Adjust to UTC+8
| where dayofweek(LocalTime) between (1d .. 5d) // Monday to Friday in LocalTime
| extend hour = datetime_part("hour", LocalTime)
| where hour between (8 .. 17) // Core hours: 8 AM to 5 PM in UTC+8
| where ObjectName == "Processor Information" and CounterName == "% Processor Time"
| where filterSubstring == "ALL" or tostring(Computer) contains filterSubstring
| extend 
    Region = case(
        Computer startswith "M01" or Computer startswith "M04", "EMEA",
        Computer startswith "M13" or Computer startswith "M14", "APAC",
        Computer startswith "M06" or Computer startswith "M09", "AMRS",
        "Other"
    ),
    Environment = case(
        substring(Computer, 3, 1) == "T", "TST", // 4th character indicates TST
        substring(Computer, 3, 1) == "U", "UAT", // 4th character indicates UAT
        substring(Computer, 3, 1) == "D", "DEV", // 4th character indicates DEV
        Computer contains "NACR", "CRIMS NonProdA",
        Computer contains "NBCR", "CRIMS NonProdB",
        Computer contains "OHCR", "OAT CRIMS High",
        Computer contains "OMCR", "OAT CRIMS Medium",
        Computer contains "OLCR", "OAT CRIMS Low",
        Computer contains "PHCR", "PRD CRIMS High",
        Computer contains "PMCR", "PRD CRIMS High",
        Computer contains "PLCR", "PRD CRIMS Low",
        substring(Computer, 3, 1) == "P", "PRD", // 4th character indicates PROD
        "Other"
    ),
    SessionHostPoolType = case(
        Computer contains "MSN", "MultiSession",
        Computer contains "SSP", "Personal",
        Computer contains "NACR", "MultiSession",
        Computer contains "NBCR", "MultiSession",
        Computer contains "OHCR", "MultiSession",
        Computer contains "OMCR", "MultiSession",
        Computer contains "OLCR", "MultiSession",
        Computer contains "PHCR", "MultiSession",
        Computer contains "PMCR", "MultiSession",
        Computer contains "PLCR", "MultiSession",
        "Other"
    ),
    Management = case(
        substring(Computer, 7, 1) == "I", "Intune",
        substring(Computer, 7, 1) == "G", "GPO",
        "Other"
    )
| summarize AVG_CPU = round(avg(CounterValue), 2), 
            MAX_CPU = round(max(CounterValue), 2), 
            MIN_CPU = round(min(CounterValue), 2),
            MED_CPU = round(percentile(CounterValue, 50), 2),
            ItemsPerTimeBin = count() // Counting the number of entries per time bin
  by BinnedTime = bin(TimeGenerated, binSize), Computer, Region, Environment, SessionHostPoolType, Management
| extend Computer = trim(' ', tolower(Computer))
| project 
    TimeGenerated = BinnedTime,
    SessionHostName = Computer,
    Region,
    Environment,
    SessionHostPoolType,
    Management,
    AVG_CPU,
    MED_CPU,
    MAX_CPU,
    MIN_CPU,
    ItemsPerTimeBin
