let binSize = 1m;
let timeRangeStart = now(-30d);
let timeRangeEnd = now();
let filterSubstring = "ALL";
Perf
| where TimeGenerated between (timeRangeStart..timeRangeEnd)
| where ObjectName == "Processor Information" and CounterName == "% Processor Time"
| where filterSubstring == "ALL" or tostring(Computer) contains filterSubstring
| summarize 
    AVG_CPU_Utilization_per = round(avg(CounterValue), 2),
    MAX_CPU_Utilization_per = round(max(CounterValue), 2),
    MIN_CPU_Utilization_per = round(min(CounterValue), 2),
    MED_CPU_Utilization_per = round(percentile(CounterValue, 50), 2),
    ItemsPerTimeBin_int = count(),
    TimeGenerated = min(TimeGenerated)  // Retaining the original TimeGenerated value
  by BinnedTime = bin(TimeGenerated, binSize), Computer, Date = format_datetime(TimeGenerated, 'yyyy-MM-dd')
| extend Time = format_datetime(BinnedTime, 'HH:mm')
| extend TimeKey = replace(":", "", Time)
| where MAX_CPU_Utilization_per >= 70
| project TimeGenerated, Date, Time, TimeKey, Computer, AVG_CPU_Utilization_per, MED_CPU_Utilization_per, MAX_CPU_Utilization_per, MIN_CPU_Utilization_per, ItemsPerTimeBin_int
