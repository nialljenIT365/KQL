CorrelationId	TimeGenerated [UTC]	UserRoute [UTC]	RdpStackConnectionEstablished [UTC]	credaquire	ssotoken	LogonDelay [UTC]	ShellStart [UTC]	ProductiveTime [UTC]
1659fed5-ee21-4c44-a18b-6082d7bc0000	22/11/2024, 15:14:49.645	22/11/2024, 15:14:50.629	22/11/2024, 15:14:53.834	17		22/11/2024, 15:15:24.068	22/11/2024, 15:15:26.435	22/11/2024, 15:15:47.193
c5043729-099d-44ac-83fe-4c7fc0ea0000	22/11/2024, 15:14:08.140	22/11/2024, 15:14:08.967	22/11/2024, 15:14:23.203	8134		22/11/2024, 15:14:45.637	22/11/2024, 15:14:48.110	22/11/2024, 15:15:20.692
6123f470-bf26-4fd0-aa92-2735d73d0000	22/11/2024, 15:16:10.918	22/11/2024, 15:16:12.168	22/11/2024, 15:16:42.613	25308		22/11/2024, 15:17:24.901	22/11/2024, 15:17:25.874	22/11/2024, 15:17:34.955
1177b0c9-e331-4b20-9e5d-bb5175fa0000	22/11/2024, 15:17:17.045	22/11/2024, 15:19:22.139	22/11/2024, 15:19:28.002	3		22/11/2024, 15:19:45.164	22/11/2024, 15:19:45.588	22/11/2024, 15:19:52.104
92a6dd85-3224-4255-8fa7-0b0e29690000	22/11/2024, 15:22:35.137	22/11/2024, 15:22:35.491	22/11/2024, 15:22:50.131	11923		22/11/2024, 15:23:08.336	22/11/2024, 15:23:09.742	22/11/2024, 15:23:27.459
80c6cdd2-e192-4444-938a-16b4429f0000	22/11/2024, 15:23:07.291	22/11/2024, 15:23:07.911	22/11/2024, 15:23:10.219	0		22/11/2024, 15:23:24.650	22/11/2024, 15:23:27.133	22/11/2024, 15:23:45.442
8eb955af-2a26-4411-b93f-6f02fbae0000	22/11/2024, 15:25:22.918	22/11/2024, 15:25:23.426	22/11/2024, 15:25:32.534	6777		22/11/2024, 15:25:44.981	22/11/2024, 15:25:46.079	22/11/2024, 15:26:03.379
eed2a3f2-5b01-4ec6-b8c7-844924290000	22/11/2024, 13:15:24.857	22/11/2024, 13:15:25.395	22/11/2024, 13:15:44.523	15246		22/11/2024, 13:16:03.961	22/11/2024, 13:16:05.241	22/11/2024, 13:16:19.849
4245a6c5-9517-4a3c-9c00-f5593d9d0000	22/11/2024, 13:17:20.034	22/11/2024, 13:17:21.138	22/11/2024, 13:17:40.141	12487		22/11/2024, 13:17:55.587	22/11/2024, 13:17:57.773	22/11/2024, 13:18:16.207
3711d11e-9d56-4bdb-9644-1afaf1710000	22/11/2024, 13:20:52.448	22/11/2024, 13:20:52.921	22/11/2024, 13:20:56.437	10		22/11/2024, 13:21:08.300	22/11/2024, 13:21:09.823	22/11/2024, 13:21:25.186



let newSessions = true;
let checkpoints =  WVDCheckpoints
    | summarize arg_min(TimeGenerated, *) by CorrelationId, Name;
let Checkpoints = (name: string) {
    checkpoints
    | where Name == name 
    | project CorrelationId, bag = pack(name, TimeGenerated)
    | evaluate bag_unpack(bag)
};
let ChartInfo = datatable (Stage: string, label: string, stageOrder: int) [
    "UserRoute", "User Route", 1,
    "TransportConnected", "Stack Connected", 3,
    "OnCredentialsAcquisitionCompleted", "creds", 4,
    "RdpStackConnectionEstablished", "Stack Connected", 5,
    "LogonDelay", "Logon", 6,
    "ShellStart", "Shell Start", 7,
    "ProductiveTime", "Shell Ready", 8
];
WVDConnections
| where State == "Started"
| where ("no_host_selected" == "no_host_selected" or trim_end("[.].*", SessionHostName) == "no_host_selected")
| distinct TimeGenerated, CorrelationId
| join kind = leftsemi
    (
    // Only include connections that actually reached the host to prevent short (failed) attempts from skewing the data
    checkpoints
    | where Source == "RDStack"
        and Name == "RdpStackConnectionEstablished"
    )
    on CorrelationId
| project CorrelationId, TimeGenerated
| join kind = inner // UserRoute
    (
    checkpoints
    | where Name == "LoadBalancedNewConnection"
    | extend LoadBalanceOutcome=Parameters.LoadBalanceOutcome
    | where (newSessions and LoadBalanceOutcome == "NewSession") or (not(newSessions) and LoadBalanceOutcome in ('Disconnected', 'Active'))
    | project CorrelationId, UserRoute=TimeGenerated
    | distinct CorrelationId, UserRoute
    )
    on CorrelationId
| project-away CorrelationId1
| join kind=innerunique Checkpoints("RdpStackConnectionEstablished") on CorrelationId
| project-away CorrelationId1
| join kind=leftouter 
    (
    checkpoints
    | where Name =~ "OnCredentialsAcquisitionCompleted"
    | project CorrelationId, credaquire = tolong(Parameters.DurationMS)
    )
    on CorrelationId
| project-away CorrelationId1
| join kind=leftouter //SSO token
    (
    checkpoints
    | where Name =~ "SSOTokenRetrieval"
    | project CorrelationId, ssotoken = tolong(Parameters.DurationMS)
    )
    on CorrelationId
| project-away CorrelationId1
| join kind=innerunique // Logon Delay
    (checkpoints
    | where Name == "LogonDelay"
    | extend LogonType = tostring(Parameters.LogonType)
    | where (newSessions and LogonType == "DirectSession") or (not(newSessions) and LogonType == "TemporarySession")
    | project LogonDelay=TimeGenerated, CorrelationId
    )
    on CorrelationId
| project-away CorrelationId1
| join kind=leftouter Checkpoints("ShellStart") on CorrelationId
| project-away CorrelationId1
| join kind = leftouter
    (
    checkpoints // new session
    | where newSessions
    | where Name =~ "ShellReady" or
        (Name =~ "LaunchExecutable" and Parameters.connectionStage == "RdpShellAppExecuted" or Name =~ "RdpShellAppExecuted")
    | project ProductiveTime=TimeGenerated, CorrelationId
    )
    on CorrelationId
| project-away CorrelationId1
| where not(newSessions) or isnotnull(ProductiveTime)
| where (datetime_diff("millisecond", UserRoute, TimeGenerated) - coalesce(ssotoken, 0)) >= 0
